#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
    xorq %rax,%rax		    # count = 0;
	iaddq $-10, %rdx		    # len >= loop unroll?
    jl Finish		        # if not, goto Finish

Loop:
    mrmovq (%rdi), %r10	    # read val from src...
    mrmovq 8(%rdi), %r11	# read val from src...
	andq %r10, %r10		    # val pos?
    jle t1
    iaddq $1,%rax
t1:
    andq %r11,%r11
    jle t2
    iaddq $1,%rax
t2:
	rmmovq %r10, (%rsi)	    # ...and store val to dst
	rmmovq %r11, 8(%rsi)	# ...and store val to dst

    mrmovq 16(%rdi), %r10	# read val from src...
    mrmovq 24(%rdi), %r11	# read val from src...
	andq %r10, %r10		    # val pos?
    jle t3
    iaddq $1,%rax
t3:
    andq %r11,%r11
    jle t4
    iaddq $1,%rax
t4:
	rmmovq %r10, 16(%rsi)	# ...and store val to dst
	rmmovq %r11, 24(%rsi)	# ...and store val to dst

    mrmovq 32(%rdi), %r10	# read val from src...
    mrmovq 40(%rdi), %r11	# read val from src...
	andq %r10, %r10		    # val pos?
    jle t5
    iaddq $1,%rax
t5:
    andq %r11,%r11
    jle t6
    iaddq $1,%rax
t6:
	rmmovq %r10, 32(%rsi)	# ...and store val to dst
	rmmovq %r11, 40(%rsi)	# ...and store val to dst

    mrmovq 48(%rdi), %r10	# read val from src...
    mrmovq 56(%rdi), %r11	# read val from src...
	andq %r10, %r10		    # val pos?
    jle t7
    iaddq $1,%rax
t7:
    andq %r11,%r11
    jle t8
    iaddq $1,%rax
t8:
	rmmovq %r10, 48(%rsi)	# ...and store val to dst
	rmmovq %r11, 56(%rsi)	# ...and store val to dst

    mrmovq 64(%rdi), %r10	# read val from src...
    mrmovq 72(%rdi), %r11	# read val from src...
	andq %r10, %r10		    # val pos?
    jle t9
    iaddq $1,%rax
t9:
    andq %r11,%r11
    jle t10
    iaddq $1,%rax
t10:
	rmmovq %r10, 64(%rsi)	# ...and store val to dst
	rmmovq %r11, 72(%rsi)	# ...and store val to dst

	iaddq $80, %rdi		    # src++
	iaddq $80, %rsi		    # dst++
    iaddq $-10, %rdx		    # len >= loop unroll?
	jge Loop			    # if so, goto Loop:

Finish:
    iaddq $10, %rdx		    # restore len
    addq %rdx, %rdx		    # compute jump destination
    mrmovq Jump_table(%rdx),%rdx
    irmovq 0xffff, %r11
    andq %r11, %rdx
    pushq %rdx
    ret                     # jump !

Len0:
    ret
Len1:
    mrmovq (%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, (%rsi)	    # ...and store val to dst
    ret
Len2:
    mrmovq (%rdi), %r10	    # read val from src...
    mrmovq 8(%rdi), %r11	    # read val from src...
	andq %r10, %r10		    # val pos?
    jle l2
    iaddq $1,%rax
l2:
	andq %r11, %r11		    # val pos?
    jle l2b
    iaddq $1,%rax
l2b:
	rmmovq %r10, (%rsi)	    # ...and store val to dst
	rmmovq %r11, 8(%rsi)	    # ...and store val to dst
    ret
Len3:
    mrmovq 16(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 16(%rsi)	    # ...and store val to dst
    jmp Len2
Len4:
    mrmovq 24(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 24(%rsi)	    # ...and store val to dst
    jmp Len3
Len5:
    mrmovq 32(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 32(%rsi)	    # ...and store val to dst
    jmp Len4
Len6:
    mrmovq 40(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 40(%rsi)	    # ...and store val to dst
    jmp Len5
Len7:
    mrmovq 48(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 48(%rsi)	    # ...and store val to dst
    jmp Len6
Len8:
    mrmovq 56(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 56(%rsi)	    # ...and store val to dst
    jmp Len7
Len9:
    mrmovq 64(%rdi), %r10	    # read val from src...
	rrmovq %rax, %r9		# copy count in r9
	iaddq $1, %r9      		# r9++ (as if val is pos)
	andq %r10, %r10		    # val pos?
	cmovg %r9, %rax			# if so update count
	rmmovq %r10, 64(%rsi)	    # ...and store val to dst
    jmp Len8
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
    .align 8
Jump_table:
    .word Len0
    .word Len1
    .word Len2
    .word Len3
    .word Len4
    .word Len5
    .word Len6
    .word Len7
    .word Len8
    .word Len9
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
